plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'groovy'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'java-gradle-plugin'

group = 'net.satago.gradle'
version = file('./src/main/bundle/version').text

repositories {
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation 'junit:junit:4.12'
    testImplementation gradleTestKit()
    testImplementation 'commons-io:commons-io:2.4'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

jar.dependsOn sourcesJar
jar.dependsOn javadocJar

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomFragments = {
    developers {
        developer {
            id "dmitrygusev"
            name "Dmitry Gusev"
            email "dmitry@anjlab.com"
        }
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()

                root.appendNode('description', project.description)

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode("name", "The Apache Software License, Version 2.0")
                license.appendNode("url", "http://www.apache.org/licenses/LICENSE-2.0.txt")
                license.appendNode("distribution", "repo")

                root.children().last() + pomFragments
            }
        }
    }
}

bintray {
    user = "$bintray_user"
    key = "$bintray_api_key"
    publications = ['pluginMaven']
    pkg {
        repo = 'maven'
        name = 'aws-candy-tools'
        userOrg = 'satago' // an optional organization name when the repo belongs to one of the user's orgs
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/satago/aws-candy-tools.git'
    }
    dryRun = false // whether to run this as dry-run, without deploying
}

task tarBundle(type: Tar) {
    archiveFileName = 'bundle.tgz'
    destinationDirectory = file('build/tar')
    from('./src/main/bundle') {
        exclude 'build/tar/'
    }
    compression Compression.GZIP
}

jar {
    manifest {
        attributes 'Implementation-Title': 'aws-candy-tools',
                'Implementation-Version': project.version
    }
    with copySpec() {
        from tarBundle.outputs
        into 'META-INF/aws-candy-tools'
    }
}

pluginUnderTestMetadata.dependsOn jar

pluginUnderTestMetadata {
    //  Run tests with final JAR file instead of extracted classes/resources
    pluginClasspath.from(
            files(jar.archiveFile),
            sourceSets.main.output.classesDirs,
            sourceSets.main.output.resourcesDir)
}
